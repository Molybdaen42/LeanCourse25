import Mathlib.MeasureTheory.Integral.IntervalIntegral.FundThmCalculus
import Mathlib.Order.CompletePartialOrder

noncomputable section
open BigOperators Function Set Real Filter Classical Topology TopologicalSpace

/-! # Exercises to hand in. -/

section GroupActions

variable (G : Type*) {X : Type*} [Group G] [MulAction G X]

/- Below is the orbit of an element `x ‚àà X` w.r.t. a group action by `G`.
Prove that the orbits of two elements are equal
precisely when one element is in the orbit of the other. -/
def orbitOf (x : X) : Set X := range (fun g : G ‚Ü¶ g ‚Ä¢ x)

lemma orbitOf_eq_iff (x y : X) : orbitOf G x = orbitOf G y ‚Üî y ‚àà orbitOf G x := by
  constructor
  ¬∑ intro h
    rw [h]
    use 1
    exact one_smul _ _
  ¬∑ intro ‚ü®g, hg‚ü©
    ext z
    constructor
    ¬∑ intro ‚ü®g', hg'‚ü©
      use g' * g‚Åª¬π
      simp [‚Üê hg', ‚Üê hg, mul_smul]
    ¬∑ intro ‚ü®g', hg'‚ü©
      use g' * g
      simp [‚Üê hg', ‚Üê hg, mul_smul]
  done

/- Define the stabilizer of an element `x` as the subgroup of elements
`g ‚àà G` that satisfy `g ‚Ä¢ x = x`. -/
def stabilizerOf (x : X) : Subgroup G where
  carrier := {g | g ‚Ä¢ x = x}
  mul_mem' {a b} ha hb := by simp_all [mul_smul]
  one_mem' := by simp
  inv_mem' {a} ha := by simp_all [inv_smul_eq_iff]

-- This is a lemma that allows `simp` to simplify `x ‚âà y` in the proof below.
@[simp] theorem leftRel_iff {x y : G} {s : Subgroup G} :
    letI := QuotientGroup.leftRel s; x ‚âà y ‚Üî x‚Åª¬π * y ‚àà s :=
  QuotientGroup.leftRel_apply

def forwardMap (x : X) : G ‚ß∏ stabilizerOf G x ‚Üí orbitOf G x :=
  Quotient.lift (fun g ‚Ü¶ (‚ü®g ‚Ä¢ x, by use g‚ü© : orbitOf G x))
    (by intro a b hab; simp_all [stabilizerOf, mul_smul, inv_smul_eq_iff])

/- Let's prove the orbit-stabilizer theorem.

Hint: Only define the forward map (as a separate definition),
and use `Equiv.ofBijective` to get an equivalence.
(Note that we are coercing `orbitOf G x` to a (sub)type in the right-hand side) -/
def orbit_stabilizer_theorem (x : X) : G ‚ß∏ stabilizerOf G x ‚âÉ orbitOf G x :=
  Equiv.ofBijective (forwardMap G x) (by
    constructor
    ¬∑ apply Quotient.ind‚ÇÇ
      intro a b hab
      simp_all [forwardMap, QuotientGroup.eq, stabilizerOf, mul_smul, inv_smul_eq_iff]
    ¬∑ intro ‚ü®a, g, hga‚ü©
      use ‚ü¶g‚üß
      simp [forwardMap, hga])

end GroupActions

section tendsto

/- Let's convince ourselves that convergence of a sequence and continuity at `x` as
defined in the lecture (and mathlib) correspond to the definitions we used in an analysis course. -/

/- Using these operations, we can define the limit. -/
def MyTendsto {X Y : Type*} (f : X ‚Üí Y)
    (F : Filter X) (G : Filter Y) :=
  map f F ‚â§ G

-- The following lemma will be very helpful.
#check mem_nhds_iff

-- You can assume this lemma for this exercise; you don't have to prove it.
-- (It is similar to the lemma `IsOpen.exists_Ioo_subset` in mathlib.)
lemma _root_.IsOpen.exists_Ioo_subset' {s : Set ‚Ñù} {x : ‚Ñù} (hs : IsOpen s) (hx : x ‚àà s) :
    ‚àÉ a b, a < b ‚àß x ‚àà Ioo a b ‚àß Ioo a b ‚äÜ s := by
  sorry

example (u : ‚Ñï ‚Üí ‚Ñù) (x : ‚Ñù) : MyTendsto u atTop (ùìù x) ‚Üî ‚àÄ Œµ > 0, ‚àÉ N, ‚àÄ n ‚â• N, |u n - x| < Œµ := by
  simp only [MyTendsto]
  constructor
  ¬∑ intro h Œµ hŒµ
    have : ‚àÉ N, ‚àÄ n ‚â• N, n ‚àà u ‚Åª¬π' (Ioo (x - Œµ) (x + Œµ)) := by
      simp_rw [le_def, mem_map, mem_atTop_sets] at h
      exact h (Ioo (x - Œµ) (x + Œµ)) (isOpen_Ioo.mem_nhds (by simp [hŒµ]))
    simp_all [abs_sub_lt_iff, sub_lt_iff_lt_add, and_comm, add_comm]
  ¬∑ intro h s hs
    -- Choose epsilon so an open interval around it is contained in s.
    have : ‚àÉ Œµ, 0 < Œµ ‚àß Ioo (x - Œµ) (x + Œµ) ‚äÜ s := by
      simp_rw [Metric.mem_nhds_iff, ball_eq_Ioo] at hs
      exact hs
    obtain ‚ü®Œµ, hŒµ, hŒµs‚ü© := this
    obtain ‚ü®N, hN‚ü© := h Œµ hŒµ
    rw [mem_map, mem_atTop_sets]
    use N
    intro n hn
    apply hŒµs
    simp_all [abs_sub_lt_iff, sub_lt_iff_lt_add, and_comm, add_comm]

-- The following exercise is a bonus exercise: any points you get here will be counted on top
-- of your regular points.
example (f : ‚Ñù ‚Üí ‚Ñù) (x : ‚Ñù) :
    Tendsto f (ùìù x) (ùìù (f x)) ‚Üî ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ x y, |x - y| < Œ¥ ‚Üí |f x - f y| < Œµ := by
  sorry

end tendsto

section indicator

#check Filter.Eventually.filter_mono
#check Filter.Eventually.mono

/- Here is a technical property using filters, characterizing when a 2-valued function converges to
a filter of the form `if q then F else G`. The next exercise is a more concrete application.
Useful lemmas here are
* `Filter.Eventually.filter_mono`
* `Filter.Eventually.mono` -/
lemma technical_filter_exercise {Œπ Œ± : Type*} {p : Œπ ‚Üí Prop} {q : Prop} {a b : Œ±}
    {L : Filter Œπ} {F G : Filter Œ±}
    (hbF : ‚àÄ·∂† x in F, x ‚â† b) (haG : ‚àÄ·∂† x in G, x ‚â† a) (haF : pure a ‚â§ F) (hbG : pure b ‚â§ G) :
    (‚àÄ·∂† i in L, p i ‚Üî q) ‚Üî
    Tendsto (fun i ‚Ü¶ if p i then a else b) L (if q then F else G) := by
  have hab : a ‚â† b := haF hbF
  rw [tendsto_iff_eventually]
  constructor
  ¬∑ intro h r hFG
    filter_upwards [h] with i hi
    rw [hi]
    by_cases hq : q
    ¬∑ simp [hq] at hFG ‚ä¢
      exact haF hFG
    ¬∑ simp [hq] at hFG ‚ä¢
      exact hbG hFG
  ¬∑ intro h
    by_cases hq : q
    ¬∑ simp [hq] at h ‚ä¢
      specialize h hbF
      filter_upwards [h] with i
      simp [hab]
    ¬∑ simp [hq] at h ‚ä¢
      specialize h haG
      filter_upwards [h] with i
      simp [hab.symm]
  done

/- To be more concrete, we can use the previous lemma to prove the following.
if we denote the characteristic function of `A` by `1_A`, and `f : ‚Ñù ‚Üí ‚Ñù` is a function,
then  `f * 1_{s i}` tends to `f * 1_t` iff `x ‚àà s i` is eventually equivalent to
`x ‚àà t` for all `x`. (note that this does *not* necessarily mean that `s i = t` eventually).
`f * 1_t` is written `indicator t f` in Lean.
Useful lemmas for this exercise are `indicator_apply`, `apply_ite` and `tendsto_pi_nhds`. -/
lemma tendsto_indicator_iff {Œπ : Type*} {L : Filter Œπ} {s : Œπ ‚Üí Set ‚Ñù} {t : Set ‚Ñù} {f : ‚Ñù ‚Üí ‚Ñù}
    (ha : ‚àÄ x, f x ‚â† 0) :
    (‚àÄ x, ‚àÄ·∂† i in L, x ‚àà s i ‚Üî x ‚àà t) ‚Üî
    Tendsto (fun i ‚Ü¶ indicator (s i) f) L (ùìù (indicator t f)) := by
  unfold indicator
  simp_rw [tendsto_pi_nhds, apply_ite ùìù]
  apply forall_congr'
  intro x
  apply technical_filter_exercise
  ¬∑ exact continuous_id.continuousAt.eventually_ne (ha x)
  ¬∑ exact continuous_id.continuousAt.eventually_ne (ha x).symm
  ¬∑ rw [pure_le_nhds_iff]
  ¬∑ rw [pure_le_nhds_iff]
  done

end indicator
